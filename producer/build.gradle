buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:1.0.1.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'

group 'com.example.messaging.producer'
version '1.0'

jar {
    baseName = 'producer'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
    compile 'org.springframework.boot:spring-boot-starter-integration'
    compile 'org.springframework.integration:spring-integration-http'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.cloud:spring-cloud-stream-test-support'
    testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR2'
    }
}

contracts {
    contractsDslDir = file "${projectDir}/src/test/resources/contracts"
    basePackageForTests = 'com.example.messaging.producer'
    packageWithBaseClasses = 'com.example.messaging.producer.base'
}

sourceSets {
    test {
        java {
            srcDirs += "${buildDir}/generated-test-sources"
        }
    }
}

test {
    testLogging {
        events = ['passed', 'failed', 'skipped']
    }
}
